/**
 * Top-level build file for ftc_app project.
 *
 * It is extraordinarily rare that you will ever need to edit this file.
 *
 * (Pssst... we totally edited this file.)
 */

buildscript {
	ext {
		androidHomePath = "$System.env.ANDROID_HOME"
		boolean haveAndroidHomePath = androidHomePath?.trim()

		if ( rootProject.hasProperty("buildAndroid") ) {
			// The caller passed -PbuildAndroid=<bool> on the commandline
			// (or used gradle.properties).  This will give the 'buildAndroid'
			// property a string type, which can run us into trouble, because
			// in Groovy, "false" != false.  We must coerce the variable into
			// being a boolean so that we get the boolean behavior we expect.
			buildAndroid = buildAndroid.toBoolean()
		}
		else {
			// Default the 'buildAndroid' parameter to true.
			buildAndroid = true
		}

		if ( !haveAndroidHomePath ) {
			println "NOTE: Environment variable ANDROID_HOME is empty."
			println "NOTE: The script will avoid building any android targets."
			println "NOTE: Android builds can also be prevented by passing -PbuildAndroid=false"
			buildAndroid = false
		}
		//else {
		//	println "NOTE: Found ANDROID_HOME path: $androidHomePath"
		//	println "NOTE: Android builds can be prevented by passing -PbuildAndroid=false"
		//}
	}
    repositories {
        jcenter()
    }
    dependencies {
		if ( rootProject.buildAndroid ) {
			classpath 'com.android.tools.build:gradle:2.1.3'
		}

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'out'
        }
    }
}
